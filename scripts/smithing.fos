#ifndef SMITHING
#define SMITHING

#include "_utils.fos"
#include "smithing_h.fos"
#include "_ltp.fos"
#include "gathering_h.fos"
#include "speed_h.fos"
#include "combat_h.fos"

class MenuAnvil: CenteredMenuHandler
{
    uint anvil_id;
    uint map_id;
	
    MenuAnvil( Item& anvil, Map& map )
	{
        anvil_id = anvil.Id;
        map_id = map.Id;
    }

    bool MenuUpdate( Critter& cr, iDialogBox& menu )
	{
        Map@ map = GetMap( map_id );
        Item@ anvil = GetItem( anvil_id );
	
		bool locked_in_place = !FLAG( anvil.Flags, ITEM_CAN_PICKUP );
	
        if( map is null || anvil is null )
		{
            return false;
        }

		if( locked_in_place )
		{
			if( hasItems( cr, SmithingTools, SLOT_HAND1 ) && menu.Button( "Smith" ) )
			{
				Item@ tool = _CritGetItemHand( cr );
				PrepareSmithing( cr, tool, anvil );
				return false;
			}

			if( menu.Button( "Examine" ) )
			{
				ShowContainer( cr, anvil, TRANSFER_HEX_CONT_UP );
				return false;
			}
			
			if( menu.Button( "Detach" ) )
			{
				LockInPlace( cr, anvil );
				return true;
			}
		}
		else
		{
			if( menu.Button( "Anchor" ) )
			{
				LockInPlace( cr, anvil );
				return true;
			}

			if( menu.Button( "Pick up" ) )
			{
				PickItem( cr, anvil );
				ChangeCritterSpeed( cr );
				return false;
			}			
		}
		return true;
    }
	
    string@ Description( Critter& cr )
	{
	
		string info;
		Item@ anvil = GetItem( anvil_id );
		
		Item@[] MaterialsCheck;
		anvil.GetItems( uint( -1 ), MaterialsCheck );
		Item@ material = null;
		
		if( MaterialsCheck.length() == 0 )
		{ 
			info = "|0x3CF800 The anvil is empty."; 
		}
		else
		{
			for( uint i = 0; i < MaterialsCheck.length(); i ++ )
			{
				uint16 materialPid = MaterialsCheck[i].GetProtoId();
				if( MaterialList.find( materialPid ) != -1 )
				{
					@material = MaterialsCheck[i];
					break;
				}
			}
		}
		
		if( !valid( material ) )
		{ 
			info = "|0x3CF800 There is no approporiate metal ingot on the anvil.";
        }
		else
		{
			string amount = 0;
			uint16 materialPid = material.GetProtoId();
			info = "|0x3CF800 There is a ";
			info += "|0xFFFF00 " + GetMsgStr( 1, TEXTMSG_ITEM, materialPid * 100 ) + "|0x3CF800  on the anvil";
			amount = "|0xFFFF00 " + material.GetCount();
			info += "|0x3CF800  - ";
			info += "|0xFFFF00 " + amount;
			info += "|0x3CF800  pcs.";
		}
		info += "\nIn order to work with tha anvil\nyou must hold some kind of sledgehammer.";
		return info;
    }
	
    string@ ButtonCancel()
	{
        return ButtonDecorator( "Hide menu", null );
    }
}

void StartMenuAnvil( Critter& cr, Item& anvil )
{
    Map@ map = cr.GetMap();
    if( map is null )
	{
        return;
    }

    iMenuHandler@ handler = MenuAnvil( anvil, map );
    iDialogBox@ menu = OpenMenu( cr, "Anvil", handler );
}

bool ltp_smithing_inited = false;
void ltp_smithing_init()
{
	LTPREG( LTP_SMITHING, process_Smithing )
	ltp_smithing_inited = true;
}

bool PrepareSmithing( Critter& cr, Item@ tool, Item@ anvil ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) )
	{
		return false;
	}
	
	if( !valid( tool ) || !valid( anvil ) || anvil.GetProtoId() != PID_ANVIL )
	{
		return false;
	}
	
	if( !hasItems( cr, SmithingTools, SLOT_HAND1 ) )
	{
		return false;
	}

	if( IsTired( cr ) )
	{
		return false;
	}
	
	if( cr.IsDmgTwoArm() || cr.IsDmgTwoLeg() )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_INJURED_CANT_WORK );
		return false; 
	}
	
	Item@[] MaterialsCheck;
	anvil.GetItems( uint( -1 ), MaterialsCheck );
	Item@ material = null;
	
	if( MaterialsCheck.length() == 0 )
	{ 
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_ANVIL_EMPTY ); 
		return false; 
	}
	else
	{
		for( uint i = 0; i < MaterialsCheck.length(); i ++ )
		{
			uint16 materialPid = MaterialsCheck[i].GetProtoId();
			if( MaterialList.find( materialPid ) != -1 )
			{
				@material = MaterialsCheck[i];
				break;
			}
		}
		
		if( !valid( material ) )
		{ 
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_INGOT ); 
			return false; 
		}
	}
	
	start_Smithing( cr, tool, anvil, material );
	return true;
}

bool start_Smithing( Critter& cr, Item& tool, Item& anvil, Item@ material )
{
	if(!ltp_smithing_inited)
	{
		ltp_smithing_init();
	}
	
	if( valid( tool ) && valid( anvil ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint[] values = { cr.Id };
		
		_CritAnimateSwing( cr );
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_SmithingSound", values, false);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", cr.Id, true);	

		StartProcess( cr, LTP_SMITHING, 0, anvil.Id, material.Id, action_pause );
		return true;
	}
	return false;
}

uint process_Smithing( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_SMITHING )
	uint hit_pause = ACTION_PAUSE_BASE - ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ anvil = GetItem( param1 );
  	Item@ material = GetItem( param2 );
  	if( valid( anvil ) && valid( material ) )
    {
		param0++;
		
		uint smithing_rate = ( cr.Stat[ ST_STRENGTH ] * ACTION_PAUSE_BONUS_RATE ) + OBJECT_DPA_BASE; 
		int sequence_length = uint( ceil( float( 3000 / ( CLAMP( smithing_rate, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) ) );
		
		if( param0 > sequence_length )
		{
			cr.SayMsg( SAY_EMOTE, TEXTMSG_TEXT, STR_FINISH_SMITHING );
			Profit( cr, anvil, material );
			int skillNum = SK_REPAIR;
			Accident( cr, skillNum );
			return 0;
		}
		
		uint[] values = { cr.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_SmithingSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "gathering@e_TiredenessTimeout", values, true);

		_CritAnimateSwing( cr );
		
		if( cr.IsInjured() )
		{
			uint damage = INJURED_HP_DAMAGE - cr.Stat[ ST_ENDURANCE ];
			InjureCritter( cr, damage, DAMAGE_UNCALLED, cr.Dir, cr.Id, HIT_LOCATION_NONE, false );
		}
		
		return action_pause;
	}
	return 0;
}

uint e_SmithingSound( uint[]@ values )
{
	Critter@ cr = GetCritter(values[0]);
	Map@ map = cr.GetMap();
	PlayGenericSound( map, cr.HexX, cr.HexY, _GetOneRandom( SmithingSounds ), 10 );
	return 0;
}

void Profit(  Critter& cr, Item& anvil, Item& material )
{
	if( !valid( anvil) && !valid( material ) )
	{
		return;
	}
	
	uint16 materialPid = material.GetProtoId();
	uint8 index = MaterialList.find( materialPid );
	uint16 resultPid = ResultList[ index ];
	uint8 amount = Amount[index];
	cr.AddItem( resultPid, amount );
	_SubItem( material, 1 );
	
	cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_YOU_CRAFTED_PID, "$proto@msg item " +  ( resultPid * 100 ) + "@$amount" + amount + "\n" );

	cr.StatBase[ST_EXPERIENCE] += 25;
}

#endif //SMITHING