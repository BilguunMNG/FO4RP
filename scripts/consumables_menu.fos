#ifndef CONSUMABLES_MENU
#define CONSUMABLES_MENU

void unsafe_menu( Critter& cr, int id, int, int, string@, int[]@ )// ~drugs
{
	Critter@ target = @cr;
	if( isGM( cr ) && id != 0 )
	{
		@target = GetCritter(id);
		if( !valid( target ) )
			@target = @cr;
	}
	StartConsumablesMenu( cr, target );
}

void StartConsumablesMenu( Critter& cr, Critter& target )//exported
{
    iDialogBox@ menu = OpenMenu( cr, "Drugs", MenuDrugs( target ) );
}


class MenuDrugs: CenteredMenuHandler
{
	Critter@ target;
	ActiveDrug@[] drugs;
	bool delayed_refresh;
	
	MenuDrugs( Critter@ target )
	{
		@this.target = @target;
		drugs = enlistAllActiveDrugs(target);
	}

	bool MenuUpdate( Critter& cr, iDialogBox& menu ) 
	{		
		for( uint i = 0, len = drugs.length(); i < len; i++ )
		{
			ActiveDrug@ drug = drugs[i];
			
			string name = drug.event_id() + " [" + drug.phase() + "]" + " '" + STR_INSERT_ITEM_LINE( drug.pid() * 100 ) + "' ";
			string time = DeltaTime_HMS_short( uint( drug.time_left() / __TimeMultiplier ) );
			
			string lex = "$description" + name + time;
			if( menu.ButtonMsg( STR_ITEMNAME, lex ) )
			{
				if( isGM( cr ) )
				{
					skipStage( target, drug );
				}
				
				drugs = enlistAllActiveDrugs(target);
				delayed_refresh = true;				 
				
				return true;
			}
		}

		return true;
	}

    int getDescriptionFile()
    {
    	return TEXTMSG_TEXT;
    }

	int getDescriptionLine()
	{
		return STR_ACTIVE_DRUGS_DESCRIPTION;
	}
	
	string@ Description( Critter& cr ) 
	{
		string info = "$list " + YELLOW;
		for( uint i = ADDICTION_BEGIN; i <= ADDICTION_END; i++ )
			if( target.Addiction[i] != 0 )
				info += "\n" + STR_INSERT_GAME_LINE( 100000 + i * 10 + 1 );
			
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		if( delayed_refresh )
		{
			delayed_refresh = false;
			drugs = enlistAllActiveDrugs(target);
			
			return true;
		}
		
		return false;
    }
}

#endif