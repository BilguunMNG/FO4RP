#ifndef CONSUMABLES_MENU
#define CONSUMABLES_MENU

void unsafe_menu( Critter& cr, int id, int, int, string@, int[]@ )// ~drugs
{
	Critter@ target = @cr;
	if( isGM( cr ) && id != 0 )
	{
		@target = GetCritter(id);
		if( !valid( target ) )
			@target = @cr;
	}
	StartConsumablesMenu( cr, target );
}

void StartConsumablesMenu( Critter& cr, Critter& target )//exported
{
    iDialogBox@ menu = OpenMenu( cr, "Drugs", MenuDrugs( target ) );
}


class MenuDrugs: CenteredMenuHandler
{
	Critter@ target;
	ActiveDrug@[] drugs;
	bool delayed_refresh;
	
	MenuDrugs( Critter@ target )
	{
		@this.target = @target;
		drugs = enlistAllActiveDrugs(target);
	}

	bool MenuUpdate( Critter& cr, iDialogBox& menu ) 
	{		
		for( uint i = 0, len = drugs.length(); i < len; i++ )
		{
			ActiveDrug@ drug = drugs[i];
			
			string item_name;
			uint pid = drug.pid();
			if( IN_RANGE( pid, ITEM_SUBTYPE_DRUG_BEGIN, ITEM_SUBTYPE_DRUG_END ) )
			{
				uint j = pid - ITEM_SUBTYPE_DRUG_BEGIN;
				item_name = STR_INSERT_TEXT_LINE( STR_DRUG_TYPE_DOPE + j );
			}
			else
			{
				item_name = STR_INSERT_ITEM_LINE( pid * 100 );
			}
			
			string name = drug.event_id() + " [" + drug.phase() + "]" + " '" + item_name + "' ";
			string time = DeltaTime_HMS_short( uint( drug.time_left() / __TimeMultiplier ) );
			
			string lex = "$description" + name + time;
			if( menu.ButtonMsg( STR_ITEMNAME, lex ) )
			{
				if( isGM( cr ) )
				{
					skipStage( target, drug );
				}
				
				drugs = enlistAllActiveDrugs(target);
				delayed_refresh = true;				 
				
				return true;
			}
		}

		return true;
	}

    int getDescriptionFile()
    {
    	return TEXTMSG_TEXT;
    }

	int getDescriptionLine()
	{
		return STR_ACTIVE_DRUGS_DESCRIPTION;
	}
	
	string@ Description( Critter& cr ) 
	{
		string info = "$list " + YELLOW;
		for( uint i = ADDICTION_BEGIN; i <= ADDICTION_END; i++ )
			if( target.Addiction[i] != 0 )
				info += "\n" + STR_INSERT_PARAM_NAME(i);
			
		return info;
	}
	
	bool ShouldRedraw( Critter& cr )
	{
		if( delayed_refresh )
		{
			delayed_refresh = false;
			drugs = enlistAllActiveDrugs(target);
			
			return true;
		}
		
		return false;
    }
}

void SendPlayerDrugsInfo( Critter& player )
{
	if( !valid( player ) )
	{
		return;
	}
	
	ActiveDrug@[] drugs = enlistAllActiveDrugs(player);
	string result = "";

	result += STR_INSERT_TEXT_LINE( STR_ACTIVE_DRUGS_DESCRIPTION );
	string info = "$list " + YELLOW;
	for( uint i = ADDICTION_BEGIN; i <= ADDICTION_END; i++ )
	{
		if( player.Addiction[i] != 0 )
		{
			info += "\n" + STR_INSERT_PARAM_NAME(i);
		}
	}
	result += info;
	result += "\n";

	for( uint i = 0, len = drugs.length(); i < len; i++ )
	{
		ActiveDrug@ drug = drugs[i];
			
		string item_name;
		uint pid = drug.pid();
		if( IN_RANGE( pid, ITEM_SUBTYPE_DRUG_BEGIN, ITEM_SUBTYPE_DRUG_END ) )
		{
			uint j = pid - ITEM_SUBTYPE_DRUG_BEGIN;
			item_name = STR_INSERT_TEXT_LINE( STR_DRUG_TYPE_DOPE + j );
		}
		else
		{
			item_name = STR_INSERT_ITEM_LINE( pid * 100 );
		}
			
		string name = drug.event_id() + " [" + drug.phase() + "]" + " '" + item_name + "' ";
		string time = DeltaTime_HMS_short( uint( drug.time_left() / __TimeMultiplier ) );
		string lex = "$description" + name + time;
		result += GetMsgStr( 0, TEXTMSG_TEXT, STR_ITEMNAME );
		result += lex;
		result += "\n";
	}
	
	ShowInfoScreen( player, result );
}

#endif