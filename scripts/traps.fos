// Traps by Jonathan Joestar for FO4RP

#include "_macros.fos"
#include "_colors.fos"


import void HookHoldAttack( Critter@ target, uint8 hardness )  from "handcuffs";
import void CriticallyInjureCritter( Critter& cr, uint dmg, uint dmgType, uint8 dir, uint attackerId, uint hitLocation ) from "combat";

void _TrapInit( Item& item, bool FirstTime )
{
	item.SetEvent( ITEM_EVENT_WALK, "_TrapWalk" );
	item.SetEvent( ITEM_EVENT_USE, "_deActivateTrap" );

	item.Val0 = Random(0, 30);
}

void _DeactivatedTrapInit( Item& item, bool FirstTime )
{
    item.SetEvent( ITEM_EVENT_USE, "_activateTrap" );
}

bool _activateTrap( Item& item, Critter& crit, Critter@ onCritter, Item@ onItem, Scenery@ onScenery )
{
	Item@ handItem = _CritGetItemHand(crit);
	if( item.Accessory != ACCESSORY_CRITTER || (valid(handItem) && handItem.Id != item.Id))
	{
		crit.Say(SAY_NETMSG, "Чтобы разложить капкан тот должен находиться у вас в руках.");
		return true;
	}
	
	Map@ map = crit.GetMap();
	
	if(!valid(map))
	{
		return true;
	}
	
	DeleteItem(item);
	item.Update();
	
	Item@ activeTrap = map.AddItem( crit.HexX, crit.HexY, PID_TRAP_ACTIVE, 1);
	activeTrap.Val0 = 10 + crit.Param[SK_TRAPS] / 100;
	activeTrap.SetEvent( ITEM_EVENT_WALK, "_TrapWalk" );
	activeTrap.SetEvent( ITEM_EVENT_SKILL, "_deActivateTrap" );

	crit.Say(SAY_EMOTE, "Устанавливает капкан");
	
	return true;
}

bool _deActivateTrap(Item& item, Critter& crit, int skill)
{
	Map@ map = crit.GetMap();
	Item@ deactivatedTrap = map.AddItem( item.HexX, item.HexY, PID_TRAP_DEACTIVATED, 1);
	map.SetText(item.HexX, item.HexY, COLOR_LGRAY, "::щелк::");
	deactivatedTrap.SetEvent(ITEM_EVENT_USE, "_activateTrap");
	
	DeleteItem(item);
	item.Update();
	
	return true;
}


void _TrapWalk(Item& item, Critter& crit, bool entered, uint8 dir)
{
	if(entered)
	{
		crit.Wait(0);
		crit.Say(SAY_EMOTE, "Наступает в капкан");

		CriticallyInjureCritter( crit, item.Val0, DAMAGE_NORMAL, dir, 0, Random(0, 1) == 0 ? HIT_LOCATION_LEFT_LEG : HIT_LOCATION_RIGHT_LEG);
		HookHoldAttack( crit, item.Val0 );
		
		Map@ map = crit.GetMap();
		map.SetText(item.HexX, item.HexY, COLOR_LGRAY, "::щелк::");
		Item@ deactivatedTrap = map.AddItem( item.HexX, item.HexY, PID_TRAP_DEACTIVATED, 1);
		deactivatedTrap.SetEvent(ITEM_EVENT_USE, "_activateTrap");
		
		DeleteItem(item);
		item.Update();
	}
}