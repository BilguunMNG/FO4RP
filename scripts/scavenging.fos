// Author: Anuri

#include "_utils.fos"
#include "_ltp.fos"
#include "STAT_MODS_H.fos"

import int  GetDeteriorationProcent( Item& item ) from "repair";
import void SetDeterioration( Item& item, int deteriorationProcent ) from "repair";
import void InjureCritter( Critter& cr, uint dmg, uint dmgType, uint8 dir, int attackerId ) from "combat";
import void BleedCritter( Critter& cr, int8 bleedValue ) from "critter_status";

const string[] JunkSounds = {"junk1.ogg", "junk2.ogg", "junk3.ogg", "junk4.ogg", "junk5.ogg", "junk6.ogg", "junk7.ogg", "junk8.ogg"};

//=============JUNK_PROPERTIES=============//
#define	RESOURCE_CAPACITY			Val1 		// тут записано макс кол-во подходов. Используется как текущее.
#define EVENT 						Val3 		// валка для записи тайм-ивента 
//=============JUNK_SPAWNERS==============//
#define JUNK_ENTIRE					( 1010 ) 	// Спавнер мусора

#define MAP_TIME_DATA   			( 1 )

const uint16[] tools = { PID_CROWBAR, PID_SHOVEL, PID_IRON_PROD, PID_MULTI_TOOL };
const uint16[] JunkPileArtList = { 2116, 2117, 2118, 2252, 2253, 2254, 2255, 2256, 2275, 2276, 2311, 2314, 2315, 8095, 8096, 8098, 8099, 16584, 16585, 16586, 16687, 16590, 16591 };
const uint16[] lootList = { PID_CRAFT_L_LINT, PID_CRAFT_M_JUNK, PID_HLAM_MEH, PID_HLAM2_MEH, PID_BANKA_MEH, PID_METAL_LIST_MEH, PID_WATER_TUBE, PID_IRON_ROD, PID_BOTTLE_GLASS, PID_EMPTY_JET, PID_MPARTS, PID_NAILS, PID_PART_OF_ROPE,
							PID_SPIKE_MEH, PID_BOLT, PID_CRAFT_L_RAGS, PID_TUBE, PID_CRAFT_M_BARS };

// LTP (long time process)
bool ltp_inited = false;
void ltp_init()
{
	LTPREG( LTP_SCAVENGING, process_scavenging )
	ltp_inited = true;
}
//exported to main.fos - checks before start
bool PrepareScavenging( Critter& cr, Item@ tool, Item@ target ) 
{
	Map@ map = cr.GetMap();
    if( !valid( map ) ) { return false; }
	if( !valid( tool ) || !valid(target) ) { return false; }
   	
	uint toolPid = tool.GetProtoId();
	if( tools.find( toolPid ) == -1 ) { return false; }
	
	uint targetPid = target.GetProtoId();
	if( targetPid != PID_JUNK_SALVAGE )	{ return false; }
	
	if( tool.CritSlot != SLOT_HAND1 ) { cr.Say( SAY_NETMSG, "|0xFFFF00 Возьмите инструмент в руку." ); return false; }

	uint timeout_limit = ( cr.Stat[ST_ENDURANCE] * PHASE_TIMEOUT_RATE ) + PHASE_TIMEOUT_BASE;
	int timeout = CLAMP( timeout_limit, PHASE_TIMEOUT_MIN, PHASE_TIMEOUT_MAX);
	if( cr.Timeout[ TO_TIREDNESS ] > REAL_SECOND( timeout ) )
	{
		cr.Say( SAY_EMOTE_ON_HEAD, "Одышка" );
		cr.Say( SAY_NETMSG, "|0xFF0000 Вы слишком устали, передохните." );
		
		if( !isGM(cr) ) { return false; }
	}
	if( cr.ParamBase[ DAMAGE_RIGHT_LEG ] >= 1 || cr.ParamBase[ DAMAGE_LEFT_ARM ] >= 1 || cr.ParamBase[ DAMAGE_LEFT_LEG ] >= 1 || cr.ParamBase[ DAMAGE_RIGHT_ARM ] >= 1 ) 
	{
		cr.Say( SAY_NETMSG, "|0xFF0000 Вы травмированы и не в состоянии добывать ресурсы." ); 	return false;
	}

	start_scavenging( cr, tool, target );
	return true;
}

bool start_scavenging( Critter& cr, Item& tool, Item& target )
{
	if(!ltp_inited) 
		ltp_init();

	if( valid( tool ) && valid( target ) )
	{	
		uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
		uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
		uint[] values = { cr.Id, tool.Id };
		CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ScavengingSound", values, true);
		CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "e_TiredenessTimeout", cr.Id, true);	
		StartProcess( cr, LTP_SCAVENGING, 0, tool.Id, target.Id, action_pause );
		return true;
	}
	return false;
}

uint process_scavenging( Critter@ cr, int& param0, int& param1, int& param2 )
{
    LTPROCESS( cr, param0, LTP_SCAVENGING )
	uint hit_pause = ACTION_PAUSE_BASE - (cr.Stat[ST_AGILITY] * ACTION_PAUSE_BONUS_RATE );
	uint action_pause = CLAMP( hit_pause, ACTION_PAUSE_MIN, ACTION_PAUSE_MAX );
    Item@ tool = GetItem( param1 );
  	Item@ target = GetItem( param2 );
  	if( valid( tool ) && valid( target ) )
    {
      	param0++;
		Item@ tool = cr.GetItem( 0, SLOT_HAND1 );
		if( valid( tool ) )
		{
			uint object_dpa = ( cr.Skill[SK_REPAIR] - 80 ) + ( cr.Stat[ST_STRENGTH] * 10 ) + OBJECT_DPA_BASE; 
			int sequence_length = ceil( float( OBJECT_HP_BASE / ( CLAMP( object_dpa, OBJECT_DPA_MIN, OBJECT_DPA_MAX ) ) ) );

			if( param0 > sequence_length )
			{
				Profit( cr, tool, target );
				Accident( cr );
			}
			else
			{
				uint toolPid = tool.GetProtoId();
				switch( toolPid ){
					case( PID_MULTI_TOOL ):
						_CritAnimateUse(cr);
						break;
					default:
						_CritAnimateSwing(cr);
						break;
				}
				uint[] values = { cr.Id, tool.Id };
				CreateTimeEvent( AFTER( REAL_MS( 700 ) ), "e_ScavengingSound", values, true);
			}

			CreateTimeEvent( AFTER( REAL_MS( 1000 ) ), "e_TiredenessTimeout", cr.Id, true);
			return action_pause;
		}
    }
	return 0;
}

uint e_ScavengingSound( uint[]@ values )
{
	Critter@ cr = GetCritter(values[0]);
	Map@ map = cr.GetMap();
	map.PlaySound( JunkSounds[ Random( 0, JunkSounds.length() -1 ) ], cr.HexX, cr.HexY, 5 );
	return 0;
}

uint e_TiredenessTimeout(uint[]@ values)
{
	Critter@cr = GetCritter( values[0] ); 
	uint fatigue_per_action = ACTION_FATIGUE_BASE - ( cr.Stat[ST_ENDURANCE] * ACTION_FATIGUE_RATE );
	uint fatigue = CLAMP( fatigue_per_action, ACTION_FATIGUE_MIN, ACTION_FATIGUE_MAX);
	cr.TimeoutBase[ TO_TIREDNESS ] = __FullSecond + cr.Timeout[ TO_TIREDNESS ] + REAL_SECOND( fatigue );
	return 0;
}

// Accident 
bool Accident( Critter& cr )
{
	if( Random( 1, 200 * SKILL_ROLL_SIMPLIFIER ) <= ( CRITICAL_FAILURE_BASE - (cr.ParamBase[ SK_REPAIR ] / 30 ) ) )
	{
		int severity = Random( 1, 100 );
		if( severity > 65 && ( Random( 1, 100 ) < ( cr.Stat[ST_LUCK] * 2 ) ) )
		{	
			cr.Say( SAY_NETMSG, "|0x606060 В последний момент Вы чудом избежали старшной травмы, но все же:" );
			severity = severity - 35;
		}

		if( severity <= 5 )
		{
			cr.Say( SAY_NETMSG, "|0xFF0000 Отлетевший осколок ударил Вас в грудь." );
			InjureCritter( cr, 15, DAMAGE_NORMAL, cr.Dir, cr.Id );
			cr.Say( SAY_EMOTE, "Потирает ушиб" );
		}			
		else if( severity  >= 6 && severity  <= 35 )	
		{			
			cr.Say( SAY_NETMSG, "|0xFF0000 Вы ошиблись и слегка рассекли руку." );
			InjureCritter( cr, 20, DAMAGE_NORMAL, cr.Dir, cr.Id );
			SETFLAG( cr.StatBase[ ST_CRSTATUS ], CR_STATUS_BLEED );
			BleedCritter( cr, 20 );
			cr.Say(SAY_EMOTE, "Вскрикивает от боли");
			cr.Say(SAY_SHOUT_ON_HEAD, "Ай");				
		}
		else if( severity  >= 36 && severity  <= 65 )				
		{			
			cr.Say( SAY_NETMSG, "|0xFF0000 Вы попали инструментом себе по ноге и рассекли плоть." );
			InjureCritter( cr, 30, DAMAGE_NORMAL, cr.Dir, cr.Id );
			BleedCritter( cr, 40 );
			SETFLAG( cr.StatBase[ ST_CRSTATUS ], CR_STATUS_HEAVY_DMG );
			string emote = cr.Param[ ST_GENDER ] == 0 ? "Схватился за окровавленную ногу" : ( cr.Param[ ST_GENDER ] == 1 ? "Схватилась за окровавленную ногу" : "Завыло от боли" );			
		}
		else if( severity  >= 66 && severity  <= 95 )				
		{			
			cr.Say( SAY_NETMSG, "|0xFF0000 Из-за неудачного движения Вы получили тяжелую производственную травму." );
			InjureCritter( cr, 40, DAMAGE_NORMAL, cr.Dir, cr.Id );
			BleedCritter( cr, 50 );
			SETFLAG( cr.StatBase[ ST_CRSTATUS ], CR_STATUS_HEAVY_DMG );
			string emote = cr.Param[ ST_GENDER ] == 0 ? "Заорал от боли" : ( cr.Param[ ST_GENDER ] == 1 ? "Заорала от боли" : "Заорало от боли" );
			cr.Say(SAY_SHOUT_ON_HEAD, "А-а-а-ааа");			
		}	
		else
		{
			cr.Say( SAY_NETMSG, "|0xFF0000 Острый обломок отлетел Вам в глаз, кровь заливает лицо." );
			InjureCritter( cr, 50, DAMAGE_NORMAL, cr.Dir, cr.Id );
			BleedCritter( cr, 50 );
			SETFLAG( cr.StatBase[ ST_CRSTATUS ], CR_STATUS_HEAVY_DMG );	
			cr.ParamBase[ DAMAGE_EYE ] = 1;				
			string emote = cr.Param[ ST_GENDER ] == 0 ? "Взвыл от дикой боли" : ( cr.Param[ ST_GENDER ] == 1 ? "Взвыла от дикой боли" : "Взвыло от дикой боли" );
			cr.Say( SAY_SHOUT_ON_HEAD, "У-А-А-А-ААА" );
		}
		return true;		
	}
	return true;
}

//Loot
bool Profit(  Critter& cr, Item@ tool, Item@ target )
{
	int amount = 0;
	if( tool.GetProtoId() == PID_MULTI_TOOL ) { amount++; }
	if( tool.GetProtoId() == PID_SHOVEL || tool.GetProtoId() == PID_IRON_PROD ) { amount--; }
	uint lootRoll = ( Random( 0, 2 ) + ( cr.ParamBase[ SK_REPAIR ] / 100 ) );
	amount+= lootRoll;
	if( amount < 1 )
	{
		cr.Say( SAY_NETMSG, "|0xFFFF00 Вы не добыли ничего полезного." );
	}
	else
	{
		for( int i = 0; i < amount; i++ ) 
		{
			ProtoItem@ loot = GetProtoItem(  lootList[ Random( 0, lootList.length() - 1 ) ] );
			cr.AddItem( loot.ProtoId, 1 );
		}
		amount++;
		cr.AddItem( PID_CRAFT_M_JUNK, 1 );
		cr.Say( SAY_NETMSG, "|0xFFFF00 Вы добыли различный мусор в количестве " +amount+ " шт." );
		DeleteItem( target );	
	}
	return true;
}
//===============JUNK_SPAWNER_MECHANIC===============//

void JunkAutoInit() //export to gameplay.fos
{    
    Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		CheckMapEvent( maps[i] );
	}
}

uint e_SpawnJunk( int[] @ values )
{
	Map @ map = GetMap( values[ 0 ] );
	if( !valid( map ) ) { return 0; }
	
	SpawnJunk( map );

	EraseTimeEvent( map.GetData( MAP_TIME_DATA ) );
	map.SetData( MAP_TIME_DATA, CreateTimeEvent( AFTER( DELAY_FOR_HARVERTABLE_LOOT_RESPAWN ), "e_SpawnJunk", values, false ) );
	return 0;
}

bool SpawnJunk( Map& map )
{
	int entireId = JUNK_ENTIRE;
	int entires = map.CountEntire( entireId );

	uint16 hx=0, hy=0;
	for( int j = 0; j < entires; j++ )
	{
		map.GetEntireCoords( entireId, j, hx, hy );
		if( map.IsHexPassed( hx, hy ) && Random( 0, 2 ) == 2 )
		{	
			Item@ JunkSalvage = map.AddItem( hx, hy, PID_JUNK_SALVAGE, 1 );
			ProtoItem@ proto = GetProtoItem(  JunkPileArtList [ Random( 0, JunkPileArtList.length() - 1 ) ] );
			JunkSalvage.PicMap = proto.PicMap;
			JunkSalvage.Update();
		}
	}
	return true;
}

//Forced Spawn
void _JunkInit(Critter& player, int param0, int param1, int param2)
{
	FoceSpawnJunk();
}

//import void FoceSpawnJunk() from "scavenging";
void FoceSpawnJunk()
{
    Map@[] maps;
    for( int i = 0, iend = GetAllMaps( 0, maps ); i < iend; i++)
	{   
		CheckMapEvent( maps[i] );
	}
}

bool CheckMapEvent( Map& map )
{
    int n = JUNK_ENTIRE;
	if( map.CountEntire( n ) != 0 ) 
	{
		int[] values = {map.Id};
        map.SetData( MAP_TIME_DATA, CreateTimeEvent( AFTER( REAL_SECOND( 1 ) ), "e_SpawnJunk", values, false ) );
		//Log("junk_event_started");
        return true;
    } 
    return false;
} 