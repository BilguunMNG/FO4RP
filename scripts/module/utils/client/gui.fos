/*********Графики и интерфейсы**************/

void DrawText( string text, Dot@ dot, int dx, int dy ) 
{ 
	if( !valid( dot ) ) return; 
	DrawText( text, dot.x + dx, dot.y + dy, dot.color ); 
}

void DrawText( string text, int x, int y, Color@ color )
{
	DrawText( text, x, y, text.length() * 10, 20, valid( color ) ? color.rgb() : COLOR_RGB( 255, 0, 0 ), FONT_FALLOUT, 0 );
}

void DrawTextAboveHex( string text, int hexX, int hexY, Color@ color, bool underline = false )
{
	int x = 0, y = 0;
	GetHexPos( hexX, hexY, x, y ); 
	
	string@[] lines;
	lines = split( text, "\n" );
	
	for( uint i = 0, l = lines.length(); i < l; i++ )
		DrawTextLine( lines[i], x, y + 10 - ( l - i ) * 20, color, underline );
}

void DrawTextLine( string text, int x, int y, Color@ color, bool underline = false )
{
	int height = 0, lines = 0, len = 0;
	GetTextInfo( text, 0, 0, FONT_FALLOUT, 0, len, height, lines );

	int x0 = x - len / 2;
	int y0 = y - 10;
	
	DrawText( text, x0, y0, len, height, valid( color ) ? color.rgb() : COLOR_RGB( 255, 0, 0 ), FONT_FALLOUT, 0 );
	
	if( underline )
		line( x0, y0 + 10, x0 + len, y0 + 10, NewColor( 255, 100, 100 ) );
}

void triangle( Dot@ dot, int size = 2 ) 
{ 
	if( !valid( dot ) || !valid( dot.color ) ) return;
	
	triangle( dot.x, dot.y, dot.color, size ); 
}

void triangle( int x, int y, Color color, int size = 2 )
{
	NewLine( "", color )
		.add( -size,  size )
		.add(  size,  size )
		.add(     0, -size )
		.draw( x, y, true );
}

void line( int x1, int y1, int x2, int y2, Color color )
{
	NewLine( "", color ).add( x1, y1 ).add( x2, y2 ).draw();
}

void line( Dot@ a, Dot@ b, Color color )
{
	NewLine( "", color ).add(a).add(b).draw();
}

void DrawLine( Line@ line, int x0 = 0, int y0 = 0, float scale = 1, bool looped = false )
{
	if( !valid( line ) || !valid( line.dots ) || line.dots.length() == 0 )
		return;

	int[] drawData;
	
	for( uint i = 0, l = line.dots.length(); i <= l; i++ )
	{
		if( !looped && i == l ) break;
		
		Dot@ dot = line.dot(i%l);

		drawData.insertLast( x0 + dot.x * scale );
		drawData.insertLast( y0 + dot.y * scale );
	
		drawData.insertLast( dot.color.inherited ? line.color.rgb() : dot.color.rgb() );
	}
	
	DrawPrimitive( DRAW_PRIMITIVE_LINESTRIP, drawData );
	
	return;
}