#ifndef AI_GUARDS
#define AI_GUARDS

#include "_utils.fos"
#include "npc_planes_h.fos"

#define MSG_GROUP_ATTACK         ( 16001 )

void GM_GuardsInit( Critter& npc ) // Export
{
    npc.SetScript( "_AiGuardsInit" );
}

void _AiGuardsInit( Critter& npc, bool firstTime )
{
	npc.SetEvent( CRITTER_EVENT_SHOW_CRITTER,	"_CritterLookAt" );
	npc.SetEvent( CRITTER_EVENT_HIDE_CRITTER,	"_CritterOutOfSight" );
    npc.SetEvent( CRITTER_EVENT_IDLE,			"_Idle" );
	npc.SetEvent( CRITTER_EVENT_MESSAGE,     	"_OnMessage" );
    npc.SetEvent( CRITTER_EVENT_PLANE_BEGIN,	"_PlaneBegin" );
	npc.SetEvent( CRITTER_EVENT_PLANE_END,		"_PlaneEnd" );
    npc.SetEvent( CRITTER_EVENT_DEAD,			"_MyDeath" );
    npc.SetEvent( CRITTER_EVENT_ATTACKED,		"_Attacked" );
	npc.SetEvent( CRITTER_EVENT_SMTH_DEAD,		"_FindKiller" );
	npc.SetEvent( CRITTER_EVENT_SMTH_ATTACK,	"_AssistAttack" );
	npc.SetEvent( CRITTER_EVENT_SMTH_ATTACKED,	"_AssistAttacked" );
}

void _CritterLookAt( Critter& npc, Critter& target )
{
	if( npc.CheckEnemyInStack( target.Id ) ) {
		if( target.IsDead() ) { 
			npc.EraseEnemyFromStack( target.Id );
			return;
		}
		AddAttackPlane( npc, 0, target );
		return;
	}
	if( Random( 0, 100 ) == 100 ) {
		uint16 hx = npc.HexX, hy = npc.HexY;
		uint16 tx = target.HexX, ty = target.HexY;
		uint dir = GetDirection( hx, hy, tx, ty );
		npc.SetDir( dir );
	}
}

void _CritterOutOfSight( Critter& npc, Critter& target )
{
	if( npc.CheckEnemyInStack( target.Id ) ) {
		npc.DropPlanes();
		if( npc.IsSee( target ) ) {
			if( target.IsDead() ) { 
				npc.EraseEnemyFromStack( target.Id );
				return;
			}
			npc.SendMessage( MSG_GROUP_ATTACK, target.Id, MESSAGE_TO_WHO_SEES_ME );
			npc.AddEnemyInStack( target.Id );
			AddAttackPlane( npc, 0, target );
		} else {
			uint16 hx = npc.HexX, hy = npc.HexY;
			uint16 tx = target.HexX, ty = target.HexY;
			int dir = GetDirection( hx, hy, tx, ty );
			AddWalkPlane( npc, AI_PLANE_ATTACK_PRIORITY - 1, target.HexX, target.HexY, dir, true, 1 );
		}
	}
}

void _Idle( Critter& npc )
{
	if( npc.IsPlayer() ) {
		npc.SetEvent( CRITTER_EVENT_IDLE, null );
		return;
	}

	if( npc.IsDead() || npc.IsKnockout() ) {
		return;
	}

	if( npc.IsBusy() || !npc.IsNoPlanes() ) {
		return;
	}
}

void _OnMessage( Critter& npc, Critter& fromCrit, int message, int value )
{
    if( npc.IsPlayer() ) { return; }
    if( npc.IsDead() ) { return; }
    if( npc.Stat[ ST_TEAM_ID ] == 0 || npc.Stat[ ST_TEAM_ID ] == 33 ) { return; }			// 0 группа не групируеться
    if( npc.Stat[ ST_TEAM_ID ] != fromCrit.Stat[ ST_TEAM_ID ] ) { return; }					// ктото не из нашей группы
 
	if( fromCrit.IsPlayer() ) { 
		AddAttackPlane( npc, 10, fromCrit ); 
	}

    if( message == MSG_GROUP_ATTACK ) {
        Critter@ target = ::GetCritter( value );
        
		if( !valid( target ) ) { return; }
		
		Map@ mapNpc = npc.GetMap();
		Map@ mapTarget = target.GetMap();
		
		if( mapNpc.Id != mapTarget.Id ) { return; }
		
		npc.AddEnemyInStack( target.Id );
		AddAttackPlane( npc, 0, target );
	}
}

int _PlaneBegin( Critter& npc, NpcPlane& plane, int reason, Critter@ someCr, Item@ someItem )
{
    if( plane.Type == AI_PLANE_ATTACK ) {
        Critter@ target = GetCritter( plane.Attack_TargId );
        if( !valid( target ) ) {
			return PLANE_RUN_GLOBAL;
		}

        if( target.Stat[ ST_TEAM_ID ] == npc.Stat[ ST_TEAM_ID ] ) {
			return PLANE_DISCARD; 
		}

     } else if ( reason == REASON_FROM_SCRIPT ) {
		return PLANE_KEEP;
	}
    return PLANE_RUN_GLOBAL;
}

int _PlaneEnd( Critter& npc, NpcPlane& plane, int reason, Critter@ someCr, Item@ someItem ) {
	return PLANE_RUN_GLOBAL;
}

void _MyDeath( Critter& npc, Critter@ killer ) 
{
    npc.ClearEnemyStack();
}

bool _Attacked( Critter& npc, Critter& target )
{
	if( int( target.Id ) == npc.StatBase [ST_FOLLOW_CRIT] ) {
        npc.EraseEnemyFromStack( target.Id );
        EraseAttackPlane( npc, target );
		return true;
	}
	npc.DropPlanes();
	npc.SendMessage( MSG_GROUP_ATTACK, target.Id, MESSAGE_TO_WHO_SEES_ME );
	npc.AddEnemyInStack( target.Id );
	AddAttackPlane( npc, 0, target );
    return true;
}

void _FindKiller( Critter& npc, Critter& fromCrit, Critter@ killer )
{
    if( npc.IsPlayer() ) { return; }
    if( npc.IsDead() ) { return; }
    if( npc.Stat[ ST_TEAM_ID ] == 0 || npc.Stat[ ST_TEAM_ID ] == 33 ) { return; }			// 0 группа не групируеться
    if( npc.Stat[ ST_TEAM_ID ] != fromCrit.Stat[ ST_TEAM_ID ] ) { return; }					// ктото не из нашей группы
 
	if( fromCrit.IsPlayer() ) { 
		AddAttackPlane( npc, 10, fromCrit ); 
	}
	
	if( npc.IsSee( killer ) ) {
		if( killer.IsDead() ) { 
			npc.EraseEnemyFromStack( killer.Id );
			return;
		}
		npc.SendMessage( MSG_GROUP_ATTACK, killer.Id, MESSAGE_TO_WHO_SEES_ME );
		npc.AddEnemyInStack( killer.Id );
		AddAttackPlane( npc, 0, killer );
	}
}

void _AssistAttack( Critter& cr, Critter& fromCr, Critter& target )
{
	if( fromCr.Stat[ ST_TEAM_ID ] == 0 || fromCr.Stat[ ST_TEAM_ID ] == 33 ) { return; }
	if( fromCr.Stat[ ST_TEAM_ID ] == cr.Stat[ ST_TEAM_ID ] ) {
		if( target.Stat[ ST_TEAM_ID ] != cr.Stat[ ST_TEAM_ID ] && !target.IsDead() ) {
			cr.SendMessage( MSG_GROUP_ATTACK, target.Id, MESSAGE_TO_WHO_SEES_ME );
			cr.AddEnemyInStack( target.Id );
			AddAttackPlane( cr, 0, target );
		}
	}
}

void _AssistAttacked( Critter& cr, Critter& fromCr, Critter& attacker )
{
	if( fromCr.Stat[ ST_TEAM_ID ] == 0 || fromCr.Stat[ ST_TEAM_ID ] == 33 ) { return; }
	if( fromCr.Stat[ ST_TEAM_ID ] == cr.Stat[ ST_TEAM_ID ] ) {
		if( attacker.Stat[ ST_TEAM_ID ] != cr.Stat[ ST_TEAM_ID ] && !attacker.IsDead() ) {
			cr.SendMessage( MSG_GROUP_ATTACK, attacker.Id, MESSAGE_TO_WHO_SEES_ME );
			cr.AddEnemyInStack( attacker.Id );
			AddAttackPlane( cr, 0, attacker );
		}
	}
}

#endif //AI_GUARDS