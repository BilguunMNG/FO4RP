#ifndef STDLIB_MODULE
#define STDLIB_MODULE

#include "_utils.fos"
#include "effects_h.fos"
#include "combat_h.fos"
#include "parameters_h.fos"
#include "speed_h.fos"

void offset( Item& item, int dx, int dy ) //exported
{
	item.OffsetX = dx;
	item.OffsetY = dy;
	UNSETFLAG( item.Flags, ITEM_FLAT );
	item.Update();
}

void SpawnBeneath( Critter& cr, uint[] pids ) //exported
{
	Map@ map = cr.GetMap();
	if( !valid(map) ) return;	

	Item@[] items; map.GetItems( cr.HexX, cr.HexY, items );
	uint[] item_pids = getPIDS(items);
	
	for( uint i = 0, l = pids.length(); i < l; i++ )
	{
		int index = item_pids.find( pids[i] );
		if( index == -1 )
		{
			items.insertLast( map.AddItem( cr.HexX, cr.HexY, pids[i], 1 ) );
			item_pids.insertLast( pids[i] );
			continue;
		}
		
		_IncItem( items[index], 1 );
	}	
}

Item@ SpawnBeneath( Critter& cr, uint pid, uint count = 1 ) //exported
{
	return SpawnItem( cr.GetMap(), cr.HexX, cr.HexY, pid, count );
}

Item@ SpawnItem( Map@ map, uint x, uint y, uint pid, uint count = 1 )
{
	if( !valid( map ) )
	{
		return null;
	}
	
	Item@[] items; map.GetItems( x, y, items );
	uint[] item_pids = getPIDS(items);
	
	int index = item_pids.find( pid );
	if( index == -1 )
	{
		return map.AddItem( x, y, pid, count );
	}
	
	_IncItem( items[index], count );
	return items[index];
}

uint[] getPIDS( Item@[] items ) 
{
	uint[] result;
	for( uint i = 0, count = items.length(); i < count; i++ )
	{
		result.insertLast( items[i].GetProtoId() );
	}
	return result;
}

bool PassableHex( Critter& cr ) //exported
{
	Map@ map = cr.GetMap();
	if( !valid( map ) )
	{
		return false;
	}
	if( !PassableHex( map, cr.HexX, cr.HexY ) )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_MOVE_TO_PLACE );
		return false;
	}
	return true;
}

bool PassableHex( Map@ map, uint x, uint y ) //exported
{
	Item@[] items;
	uint count = map.GetItems( x, y, items );
	for( uint i = 0; i < count; i++ )
	{
		if( !FLAG( items[i].Flags, ITEM_NO_BLOCK ) )
		{
			return false;
		}
	}
	return true;
}

bool CanCatch( Critter& cr, Item& item ) //exported
{
	if( isGM( cr ) )
	{
		return true;
	}
	
	uint freeWeight = cr.Stat[ ST_CARRY_WEIGHT ] - ItemsWeight( cr );
	if( freeWeight < ItemsWeight( item, 1 ) )
	{
		return false;
	}
	
	int volume = cr.ItemsVolume() + item.Proto.Volume;
	if( volume > cr.Param[ST_MAX_VOLUME] )
	{
		return false;
	}
	
	return true;
}

//Pick up items, max weight and volume check
bool PickItem( Critter& cr, Item& targetItem ) //exported
{	
	Item@ item = targetItem;
	if( !FLAG( item.Flags, ITEM_CAN_PICKUP ) )
	{
		if( targetItem.GetProtoId() == PID_POT_ALCO )
		{
			UNSETFLAG( targetItem.Flags, ITEM_CAN_USE );
			SETFLAG( targetItem.Flags, ITEM_CAN_PICKUP );
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_ITEM_DETACHED );
		}
		else
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_CANT_PICK_UP );
			return false;
		}
	}	
	
	uint freeWeight = cr.Stat[ ST_CARRY_WEIGHT ] - ItemsWeight( cr );
	
	if( !isGM( cr ) && !item.IsStackable() && ItemsWeight( targetItem ) > freeWeight )
	{
		cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_TOO_HEAVY );
		LogItemMovement( cr, targetItem, "can't pick up item (" + cr.HexX + ", " + cr.HexY + ") because it's too heavy." );
		return false;
	}
	
	uint amount1 = item.GetCount();
	if( !isGM( cr ) && freeWeight < item.Proto.Weight * item.GetCount() )
	{
		amount1 = 0;
		if( item.IsStackable() && freeWeight >= item.Proto.Weight )
		{
			amount1 = freeWeight / item.Proto.Weight;
		}	
	}
	
	uint freeVolume = cr.Param[ST_MAX_VOLUME] - cr.ItemsVolume();
	uint amount2 = item.GetCount();
	if( !isGM( cr ) && freeVolume < item.Proto.Volume * item.GetCount() )
	{
		amount2 = 0;
		if( item.IsStackable() && freeVolume >= item.Proto.Volume )
		{
			amount2 = freeVolume / item.Proto.Volume;
		}
	}
	
	if( amount1 * amount2 != 0 )
	{
		MoveItem( item, MIN( amount1, amount2 ), cr );		
		ChangeCritterSpeed( cr );
		LogItemMovement( cr, targetItem, "picks up item (" + cr.HexX + ", " + cr.HexY + ")" );		
	}
	else
	{
		if( amount1 == 0 )
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_FREE_WEIGHT );
			LogItemMovement( cr, targetItem, "can't pick up item (" + cr.HexX + ", " + cr.HexY + ") because it's too heavy." );
		}
		else 
		{
			cr.SayMsg( SAY_NETMSG, TEXTMSG_TEXT, STR_NO_FREE_VOLUME );
			LogItemMovement( cr, targetItem, "can't pick up item (" + cr.HexX + ", " + cr.HexY + ") because it's too large." );
		}
	}
	
	return true;
}

bool isWorkplace( Item& item ) //exported
{
	return isWorkplace( item.GetProtoId() );
}

bool isWorkplace( uint pid ) //exported
{
	uint[] pids = { PID_FIREBARREL, PID_STOVE_01, PID_STOVE_02, PID_STOVE_03, PID_STOVE_04, PID_STOVE_05, PID_STOVE_06, PID_STOVE_07, PID_STOVE_08, PID_FIREPLACE };
	return _Contains( pids, pid );
}

bool isDisabled( Item& item ) //exported
{
	return item.Val0 == 0;
}


void KnockoutOnce( Critter& cr, uint AP ) //exported
{
	KnockoutOnce( cr, Random( 0, 1 ) == 1, AP );
}

void KnockoutOnce( Critter& cr, bool front, uint AP ) //exported
{
	KnockoutOnce( cr, front, AP, cr.HexX, cr.HexY );
}

void KnockoutOnce( Critter& cr, bool front, uint AP, uint16 x, uint16 y ) //exported
{
	if( cr.IsKnockout() )
	{
		return;
	}
	
	cr.ToKnockout( KNOCKOUT_ANIM2_DEFAULT( front ), AP, x, y );
}

int RollVsSkill( Critter& cr, uint skillIndex, int bonus )
{
    int skVal = cr.Skill[ skillIndex ];
    int r = Random( 1, 100 );

    if( skVal + bonus >= r )
    {
        if( Random( 1, 100 ) <= ( ( ( skVal - r ) / 10 ) + cr.StatBase[ ST_CRITICAL_CHANCE ] ) )
		{
            return ROLL_CRITICAL_SUCCESS;
        }
		else
		{
            return ROLL_SUCCESS;
		}
	}
    else
    {
        if( Random( 1, 100 ) <= ( r - skVal ) / 10 )
		{
            return ROLL_CRITICAL_FAILURE;
        }
		else
		{
            return ROLL_FAILURE;
		}
	}
}

bool IsSuccessRvS( int val )
{
    return val == ROLL_CRITICAL_SUCCESS || val == ROLL_SUCCESS;
}

bool IsCriticalRvS( int val )
{
    return val == ROLL_CRITICAL_SUCCESS || val == ROLL_CRITICAL_FAILURE;
}

void HealCritter( Critter& player )
{
    if( player.IsDead() )
	{
        player.ToLife();
    }
	
	player.StatBase[ ST_CURRENT_HP ] = ( player.Stat[ ST_MAX_LIFE ] + player.Stat[ ST_MAX_LIFE_EXT ] );
}

bool TransitToLoc( Critter& cr, uint16 locPid, uint mapIndex, uint entranceNum )
{
    Location@ loc = GetLocationByPid( locPid, 0 );
    if( !valid( loc ) )
	{
        return false;
	}
	
    Map@ map = loc.GetMapByIndex( mapIndex );
    if( !valid( map ) )
	{
        return false;
	}
	
    FlushScreen( cr, true, 3000 );
    return cr.TransitToMap( map.Id, entranceNum );
}

bool TransitToMap( Critter& cr, uint16 mapPid, uint entranceNum )
{
    Map@ map = GetMapByPid( mapPid, 0 );
    if( !valid( map ) )
	{
        return false;
	}
	
    FlushScreen( cr, true, 3000 );
    return cr.TransitToMap( map.Id, entranceNum );
}

bool TransitToNewLocation( Critter& cr, uint16 locPid )
{
    Critter@[] group = { cr };

    uint locId = CreateLocation( locPid, cr.WorldX, cr.WorldY, group );
    if( locId == 0 )
	{
        return false;
	}
	
    Location@ loc = GetLocation( locId );
    loc.Visible = false;
    loc.GeckVisible = false;
    loc.AutoGarbage = true;

    FlushScreen( cr, true, 3000 );
    return cr.TransitToMap( loc.GetMapByIndex( 0 ).Id, 0 );
}

void InfoMessage( Critter& player, Critter& npc, uint strNum )
{
    player.SayMsg( SAY_NETMSG, TEXTMSG_DLG, DLGSTR( npc.Stat[ ST_DIALOG_ID ], strNum ) );
}

void NpcMessageNorm( Critter& npc, uint strNum )
{
    npc.SayMsg( SAY_NORM_ON_HEAD, TEXTMSG_DLG, DLGSTR( npc.Stat[ ST_DIALOG_ID ], strNum ) );
}

void NpcMessageShout( Critter& npc, uint strNum )
{
    npc.SayMsg( SAY_SHOUT_ON_HEAD, TEXTMSG_DLG, DLGSTR( npc.Stat[ ST_DIALOG_ID ], strNum ) );
}

void NpcMessageWhisp( Critter& npc, uint strNum )
{
    npc.SayMsg( SAY_WHISP_ON_HEAD, TEXTMSG_DLG, DLGSTR( npc.Stat[ ST_DIALOG_ID ], strNum ) );
}

void NpcMessageEmo( Critter& npc, uint strNum )
{
    npc.SayMsg( SAY_EMOTE_ON_HEAD, TEXTMSG_DLG, DLGSTR( npc.Stat[ ST_DIALOG_ID ], strNum ) );
}

void TimeoutSet( Critter& npc, int timeoutNumber, uint gameMinutes )
{
    int variable = 0;
    switch( timeoutNumber )
    {
		case( 0 ):
			variable = LVAR_timeout;
			break;
		case( 1 ):
			variable = LVAR_timeout1;
			break;
		case( 2 ):
			variable = LVAR_timeout2;
			break;
		case( 3 ):
			variable = LVAR_timeout3;
			break;
		case( 4 ):
			variable = LVAR_timeout4;
			break;
		case( 5 ):
			variable = LVAR_timeout5;
			break;
		case( 6 ):
			variable = LVAR_timeout6;
			break;
		case( 7 ):
			variable = LVAR_timeout7;
			break;
		case( 8 ):
			variable = LVAR_timeout8;
			break;
		default:
			break;
    }
    GameVar@ t = GetLocalVar( variable, npc.Id );
    t = __FullSecond + gameMinutes * 60;
}

void TimeoutSub( Critter& npc, uint gameMinutesSub )
{
    GameVar@ t = GetLocalVar( LVAR_timeout, npc.Id );
    t -= gameMinutesSub * 60;
}

void TimeoutAdd( Critter& npc, uint gameMinutesAdd )
{
    GameVar@ t = GetLocalVar( LVAR_timeout, npc.Id );
    t += gameMinutesAdd * 60;
}

void GroupSetVar( Critter& player, uint16 varId, int value )
{
    Critter@[] group = { player };
    player.GetFollowGroup( FIND_ALL, group );
    for( uint i = 0; i < group.length(); i++ )
    {
        GameVar@ var = GetLocalVar( varId, group[ i ].Id );
        if( !valid( var ) )
		{
            return;
        }
		var = value;
    }
}

void Warn( Critter& player, int npcRole )
{
    if( !player.IsPlayer() )
	{
        return;
	}
	
    Critter@ npc = GetNearCritterWithRole( player.GetMap(), player.HexX, player.HexY, WARN_DIST, npcRole );
    if( !valid( npc ) )
	{
        return;
	}
	
    npc.SayMsg( SAY_NORM_ON_HEAD, TEXTMSG_TEXT, 1001 );
}

void DoorControl( Critter& player, uint entireNum, bool toOpen )
{
    Map@   map = player.GetMap();
    uint16 x = 0, y = 0;
    if( valid( map ) && map.GetEntireCoords( entireNum, 0, x, y ) )
    {
        Item@ door = map.GetDoor( x, y );
        if( valid( door ) )
        {
            if( toOpen )
			{
                door.LockerOpen();
			}
			else
			{
                door.LockerClose();
			}
		}
    }
}

bool DialogNpc( Critter& player, int role )
{
    if( !player.IsPlayer() )
	{
        return false;
	}
	
    Critter@ npc = GetNearCritterWithRole( player.GetMap(), player.HexX, player.HexY, WARN_DIST, role );
    if( !valid( npc ) )
	{
        return false;
	}
	
    player.Wait( 0 );
    return RunDialog( player, npc, true );
}

Critter@ GetNearCritterWithRole( Map@ map, uint16 hexX, int hexY, uint radius, int role )
{
    Critter@[] critters;
    uint count = map.GetCrittersHex( hexX, hexY, radius, FIND_LIFE, critters );
    uint minDist = 10000;
    Critter@ nearCrit = null;
    for( uint i = 0; i < count; i++ )
    {
        if( critters[ i ].StatBase[ ST_NPC_ROLE ] == role )
        {
            uint d = GetDistantion( hexX, hexY, critters[ i ].HexX, critters[ i ].HexY );
            if( d < minDist )
            {
                minDist = d;
                @nearCrit = critters[ i ];
            }
        }
    }
    return nearCrit;
}

bool TransitToMap( Critter& cr, int locPid, int mapIndex, int entranceNum )
{
    Location@ location = GetLocationByPid( locPid, 0 );

    if( !valid(location) )
    {
        Log( "Location with given PID (" + locPid + ") isn't found" );
        return false;
    }

    Map@ map = location.GetMapByIndex( mapIndex );

    if( !valid(map) )
    {
        Log( "Map with given index (" + mapIndex + ") isn't found on location with PID (" + locPid + ")" );
        return false;
    }

    if( cr.IsPlayer() )
    {
        FlushScreen( cr, true, 3000 );
    }

    return cr.TransitToMap( map.Id, entranceNum );
}

#endif // STDLIB_MODULE